// $Id$

project (CUTS_Node_Daemon_Xml) : xsc_cxx {
  custom_only = 1

  Modify_Custom (XSC) {
    header_outputext = .h
    inline_outputext = .inl
  }

  specific {
    xscflags += --cxx-header-suffix .h --cxx-source-suffix .cpp \
                --cxx-inline-suffix .inl --cxx-generate-inline \
                --cxx-char-type char
  }

  Xsc_Files {
    gendir = .
    $(CUTS_ROOT)/etc/schemas/cutsnode.xsd
  }
}

project (CUTS_Node_Daemon) : boost_base, xscrt, cuts_utils, cutsexe {
  exename = cutsnode_d
  after  += CUTS_Node_Xml

  Source_Files {
    cutsnode.cpp
    cutsnode_d.cpp
    Delay_Handler.cpp
    Node_Daemon.cpp
    Node_Daemon_Options.cpp
    Node_File_Reader.cpp
    Process_Dependency_Graph.cpp
    Process_Options.cpp
    Virtual_Env.cpp
  }

  Header_Files {
  }

  specific (prop:windows) {
    compile_flags += /wd4355
  }
}
