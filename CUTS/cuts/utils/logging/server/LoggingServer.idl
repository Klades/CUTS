// -*- IDL -*-

//=============================================================================
/**
 * @file        server.idl
 *
 * $Id$
 *
 * @author      James H. Hill
 */
//=============================================================================

#ifndef _CUTS_TEST_LOGGING_SERVER_IDL_
#define _CUTS_TEST_LOGGING_SERVER_IDL_

#include "../logging.idl"

module CUTS
{
  /**
   * @interface LoggingServerListener
   *
   * Interface for callback objects of the test logger server.
   */
  interface LoggingServerListener
  {
    /**
     * Handle a collection of messages from a specific host for the
     * specified test.
     *
     * @param[in]     hostname          Host sending messages
     * @param[in]     test              Test UUID associated with messages
     * @param[in]     messages          Collection of messages
     */
    void handle_messages (in string hostname,
                          in ::CUTS::UUID test,
                          in ::CUTS::LogMessages messages);
  };

  /**
   * @interface LoggingServer
   */
  interface LoggingServer
  {
    /**
     * Register a listener for a test.
     *
     * @param[in]       uuid        UUID of test to register for
     * @param[in]       listener    Listener to receive messages
     * @return          Registration UUID
     */
    ::CUTS::UUID register_listener (in ::CUTS::UUID test,
                                    in ::CUTS::LoggingServerListener listener);

    /**
     * Unregister a listener.
     *
     * @param[in]       test        UUID of test to unregister
     * @param[in]       cookie      Listener to receive messages
     */
    void unregister_listener (in ::CUTS::UUID test,
                              in ::CUTS::UUID cookie);

    /**
     * Handle a collection of messages from a specific host for the
     * specified test.
     *
     * @param[in]     hostname          Host sending messages
     * @param[in]     test              Test UUID associated with messages
     * @param[in]     messages          Collection of messages
     */
    oneway void send_messages (in string hostname,
                               in ::CUTS::UUID test,
                               in ::CUTS::LogMessages messages);
  };
};

#endif  // !defined _CUTS_TEST_LOGGING_SERVER_IDL_
