// $Id$

project (GNC_Xsc_Gen) : game, requires_naomi, xsc_cxx {
  custom_only = 1

  specific {
    xscflags += --cxx-header-suffix .h --cxx-source-suffix .cpp \
                --cxx-inline-suffix .inl --cxx-generate-inline \
                --cxx-char-type char --cxx-generate-writer-types
  }

  Modify_Custom (XSC) {
    header_outputext = .h
    inline_outputext = .inl
  }

  Xsc_Files {
    gendir = .

    $(NAOMI_ROOT)/schemas/attribute.xsd
    $(NAOMI_ROOT)/schemas/interface.xsd
  }
}

project (GNC_Base) : requires_naomi, xscrt, cuts_gme, cutslib {
  sharedname   =  GNC_Base
  dynamicflags += GNC_BASE_BUILD_DLL

  after += GNC_Xsc_Gen

  Header_Files {
    GNC_Base_export.h
    Resource_List.h
  }

  Source_Files {
    attribute.cpp
    interface.cpp

    Attribute_Tag.cpp
    Naomi_Parser.cpp
    Naomi_Generator.cpp
  }
}

project (GNC) : requires_naomi, boost_base, cuts_gme, xscrt, cutsexe {
  exename = gnc

  after += GNC_Base

  after -= CUTS
  libs  -= CUTS

  specific (vc6, vc7, vc71, vc8, vc9) {
    subsystem = 1
  }

  Source_Files {
    attribute.cpp
    interface.cpp

    GNC.cpp
    GNC_App.cpp
    URI_Tag_Parser.cpp
  }
}
