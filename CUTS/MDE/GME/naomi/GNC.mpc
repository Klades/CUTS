// $Id$

project (GNC_Base) : requires_naomi, xsc_cxx {
  custom_only = 1

  specific {
    xscflags += --cxx-header-suffix .h --cxx-source-suffix .cpp \
                --cxx-inline-suffix .inl --cxx-generate-inline \
                --cxx-char-type char --cxx-generate-writer-types
  }

  Modify_Custom (XSC) {
    header_outputext = .h
    inline_outputext = .inl
  }

  Xsc_Files {
    gendir = .

    $(NAOMI_ROOT)/schemas/attribute.xsd
    $(NAOMI_ROOT)/schemas/interface.xsd
  }
}

project (GNC) : requires_naomi, boost_base, cuts_gme, xscrt, cutsexe {
  exename = gnc

  after += GNC_Base
  after -= CUTS
  libs  -= CUTS

  Source_Files {
    GNC.cpp
    GNC_App.cpp
    URI_Tag_Parser.cpp

    attribute_simple.cpp
    interface.cpp
  }
}
