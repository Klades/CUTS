// $Id$

project (CUTS_PICML_Base) : xsc_cxx {
  custom_only = 1

  specific {
    xscflags += --cxx-header-suffix .h --cxx-source-suffix .cpp \
                --cxx-inline-suffix .inl --cxx-generate-inline \
                --cxx-char-type char --cxx-generate-extended-rtti
  }

  Modify_Custom (XSC) {
    header_outputext = .h
    inline_outputext = .inl
  }

  Xsc_Files {
    gendir = .
    $(CUTS_ROOT)/etc/schemas/modelgen.xsd
  }
}

project (CUTS_PICML) : xscrt, picmllib, boost_base, cuts_gme, \
                       picml_udm, udm_component, gme_interpreter, cuts_cosmic {

  sharedname = CUTS_PICML

  specific {
    midl_notlb = 1
  }

  specific (vc71, vc8, vc9) {
    compile_flags += /wd4355
  }

  // -- dependencies
  after += CUTS_BE CUTS_PICML_Base
  libs  += CUTS_BE

  Source_Files {
    Main_Dialog.cpp
  }

  Source_Files {
    no_pch = 1

    modelgen.cpp
  }

  //== configuration files for the interpreter
  Define_Custom (Config) {
    automatic = 0
    inputext  = .config
  }

  Config_Files {
    $(CUTS_ROOT)/bin/cuts.config
    $(CUTS_ROOT)/bin/cuts.user.config
  }
}
