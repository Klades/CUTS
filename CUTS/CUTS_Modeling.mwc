// $Id$

///=============================================================================
/// @file         CUTS_CoSMIC.mwc
///
/// @author       James H. Hill
///
/// @brief        This workspace builds only the modeling portion
///               of CUTS.
///=============================================================================

workspace (CUTS_Modeling) {
  cmdline += -include $CUTS_ROOT/MPC/config \
             -include $XSC_ROOT/MPC/config \
             -include $CUTS_ROOT/contrib/CCF/MPC/config \
             -feature_file $CUTS_ROOT/default.features

  exclude (wix) {
    MDE/utils

    gems {
      cmdline += -base requires_gems

      MDE/GEMS
    }
  }

  exclude (!prop:windows) {
    cmdline += -include $COSMIC_ROOT/MPC/config \
               -include $GAME_ROOT/MPC/config

    MDE/GME/plugins
    MDE/GME/paradigms/CHAOS

    exclude (wix) {
      MDE/GME/paradigms/CUTS

      // == temp disable
      //MDE/GME/T2M
    }

    cosmic {
      cmdline += -base requires_cosmic

      MDE/GME/paradigms/PICML

      exclude (wix) {
        cmdline += -static

        $(CUTS_ROOT)/contrib/CCF/CCF/CodeGenerationKit
      }

      // == include the following projects
      $(COSMIC_ROOT)/PIM/PICML/interpreters/PICML
      $(GAME_ROOT)/GAME.mwc
    }
  }

  cuts/utils/utils.mpc
}
